#require "mahiru_union.eflint". 

// 1. The `local-site` indicates on which site 'this' eflint instance is running
Fact local-site Identified by site

// 2. Rule: the `local-site` is the owner of the local assets
Extend Fact owner-of Derived from owner-of(local-site.site, asset) 

// 3. Asset can be accessed when it is selected for a project that has been approved and consent has been given
Extend Act access // params: site, site', asset
  Holds when (Exists project: 
        approved(member=site,project=project)
    &&  selected(dataset=asset,project=project)
    && (Forall donor: derived-consent(controller=local-site.site,subject=donor,purpose=project)))

// 4. Rule: 'consent for DIPGResearch is consent for all DIPG projects'
Fact derived-consent Identified by subject * controller * purpose
  Holds when consent(subject,controller,purpose) // any consent is considered 'derived consent'
            ,project(purpose) && consent(subject,controller,DIPGResearch)

// 5. Allow access to third-parties when permission is given by committee
Extend Act access // params: site, site', asset
  Holds when written-consent(thirdparty=site,researcher=site',dataset=asset)
